<job>
<runtime>
<description>Remote profile removal tool</description>
<example>cscript Remote_Profile_Remover.wsf /id:[0-9][0-9]$</example>
<named name="l" helpstring="List only, do not delete (what-if mode)" />
<named name="u" helpstring="Unattended (no confirmation)" />
<named name="q" helpstring="Quiet (no output and no confirmation)" />
<named name="p" helpstring="Prompt for confirmation before deleting each profile" />
<named name="r" helpstring="Delete local caches of roaming profiles only, not local profiles" />
<named name="c" helpstring="Delete on remote computer instead of local machine" type="string" />
<named name="d" helpstring="Delete only profiles not used in x days" type="string" />
<named name="i" helpstring="Ignore errors, continue deleting" />
<named name="ed" helpstring="Exclude profile directories whose name matches this pattern. May be used more than once and can be combined with /id" type="string" />
<named name="id" helpstring="Include only profile directories whose name matches this pattern. May be used more than once and can be combined with /ed" type="string" />
<named name="regex" helpstring="Enable regex matching (default: glob with ? and *)" />
</runtime>
<script language="VBScript">
Option Explicit

Function CompilePattern(pattern)
	If rx Then
		CompilePattern = pattern
	Else 'glob
		Dim i
		CompilePattern = ""
		For i = 1 To Len(pattern)
			Dim c
			c = Mid(pattern, i, 1)
			Select Case c
			Case "?"
				CompilePattern = CompilePattern & "."
			Case "*"
				CompilePattern = CompilePattern & ".*"
			Case "[", "]", "\", "^", "$", "+", ".", "(", ")", "|", "{", "}"
				CompilePattern = CompilePattern & "\" & c
			Case Else
				CompilePattern = CompilePattern & c
			End Select
		Next 'i
	End If
End Function

Dim list, unattended, quiet, prompt, roaming, computer, days, ini, ignore, rx
With WScript.Arguments.Named
	list = .Exists("l")
	unattended = .Exists("u")
	quiet = .Exists("q")
	prompt = .Exists("p")
	roaming = .Exists("r")
	computer = .Item("c")
	If computer = "" Then computer = "."
	days = CInt(.Item("d"))
	ignore = .Exists("i")
	rx = .Exists("regex")
End With

If WScript.Arguments.Unnamed.Count <> 0 Or unattended And prompt Or quiet And prompt Then
	WScript.Arguments.ShowUsage
	WScript.Quit 1
End If
unattended = unattended Or quiet 'Quiet (no output and no confirmation)

Function Ask(prompt)
	If unattended Then
		Ask = True
	Else
		WScript.Echo prompt & " (Yes/No) "
		Dim answer
		answer = LCase(WScript.StdIn.ReadLine)
		Ask = answer = "y" Or answer = "yes"
	End If
End Function

Function CompilePatterns(arg)
	Dim prefix
	prefix = "/" & arg & ":"
	Dim o
	Set o = New RegExp
	Dim a
	For Each a In WScript.Arguments
		If Left(a, Len(prefix)) = prefix Then
			If o.Pattern <> "" Then o.Pattern = o.Pattern & "|"
			o.Pattern = o.Pattern & CompilePattern(Mid(a, Len(prefix) + 1))
		End If
	Next 'a
	Set CompilePatterns = o
End Function

Dim excluded, included
Set excluded = CompilePatterns("ed")
Set included = CompilePatterns("id")

Sub Report(s)
	If Not quiet Then WScript.Echo s
End Sub

Function OldEnough(profile)
	If IsNull(profile.LastUseTime) Then
		OldEnough = True
	Else
		OldEnough = profile.LastUseTime < DateAdd("d", -days, Now)
	End If
End Function

Dim last_error

Sub Cleanup(computer)
	Dim wmi, col, profile
	Set wmi = GetObject("winmgmts:\\" & computer & "\root\cimv2")
	'WQL doesn't handle NOT
	Set col = wmi.ExecQuery("SELECT * FROM Win32_UserProfile WHERE Special=FALSE",,48)
	For Each profile In col
		Dim name
		name = profile.LocalPath 'FIXME resolve profile.SID the same way as delprof2
		If Not included.Test(name) Then
			Report name & ": not included"
		ElseIf excluded.Pattern <> "" And excluded.Test(name) Then
			Report name & ": excluded"
		ElseIf profile.Loaded Then
			Report name & ": loaded"
		ElseIf roaming And Not profile.RoamingConfigured Then
			Report name & ": not roaming"
		ElseIf Not OldEnough(profile) Then
			Report name & ": not old enough"
		ElseIf list Then
			Report name & ": would be deleted without /l" 'technically wrong
			'... but we don't want to ask useless questions
		ElseIf Not Ask("Delete " & name & "?") Then
			Report name & ": user refused deletion"
		Else
			On Error Resume Next
			profile.Delete_
			If Err Then
				last_error = Err.number
				Report name & ": " & Err.Description
				If Not ignore Then WScript.Quit Err.number
			Else
				Report name & ": deleted"
			End If
		End If
	Next 'profile
End Sub


last_error = 0
If list Then
	Report "Listing inactive profiles on '" & computer & "'."
ElseIf Not Ask("Delete inactive profiles on '" & computer & "'?") Then
	WScript.Quit 0
End If
Cleanup computer
WScript.Quit last_error
</script>
</job>
